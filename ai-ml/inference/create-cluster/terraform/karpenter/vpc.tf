data "aws_availability_zones" "available" {
  filter {
    name   = "zone-type"
    values = ["availability-zone"]
  }
}

locals {
  vpc_cidr = "10.0.0.0/16"

  public_subnets_cidrs  = ["10.0.0.0/20", "10.0.16.0/20", "10.0.32.0/20"]
  private_subnets_cidrs = ["10.0.48.0/20", "10.0.64.0/20", "10.0.80.0/20"]

  # AZs that don't support the EKS control plane. AZ IDs are stable across accounts — AZ names are
  # randomized per-account, so filtering by name silently misses the constraint.
  # See https://docs.aws.amazon.com/eks/latest/userguide/network-reqs.html#network-requirements-subnets.
  excluded_zone_ids = ["use1-az3", "usw1-az2", "cac1-az3"]

  available_azs = [
    for i, name in data.aws_availability_zones.available.names :
    name if !contains(local.excluded_zone_ids, data.aws_availability_zones.available.zone_ids[i])
  ]

  azs = slice(local.available_azs, 0, length(local.private_subnets_cidrs))
}

# VPC module doesn't yet support regional NGW via `availability_mode = "regional"`.
# Disable the module's NGW and create an explicit one below.
# See https://github.com/terraform-aws-modules/terraform-aws-vpc/pull/1270.
module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "6.6.1"

  name = var.deployment_name
  cidr = local.vpc_cidr
  azs  = local.azs

  public_subnets  = local.public_subnets_cidrs
  private_subnets = local.private_subnets_cidrs

  enable_nat_gateway = false

  public_subnet_tags = {
    "kubernetes.io/role/elb" = 1
  }

  private_subnet_tags = {
    "kubernetes.io/role/internal-elb" = 1
    "karpenter.sh/discovery"          = var.deployment_name
  }
}

resource "aws_nat_gateway" "regional" {
  vpc_id            = module.vpc.vpc_id
  availability_mode = "regional"
  tags              = { Name = "${var.deployment_name}-ngw" }

  depends_on = [module.vpc]
}

resource "aws_route" "private_ngw" {
  count                  = length(local.private_subnets_cidrs)
  route_table_id         = module.vpc.private_route_table_ids[count.index]
  destination_cidr_block = "0.0.0.0/0"
  nat_gateway_id         = aws_nat_gateway.regional.id
}

resource "aws_security_group" "shared" {
  name        = "${var.deployment_name}-shared"
  description = "Intra-VPC shared SG; self-ingress + all egress."
  vpc_id      = module.vpc.vpc_id

  tags = {
    Name                                           = "${var.deployment_name}-shared"
    "karpenter.sh/discovery"                       = var.deployment_name
    "kubernetes.io/cluster/${var.deployment_name}" = "owned"
  }
}

resource "aws_vpc_security_group_ingress_rule" "shared_self" {
  description                  = "Self-ingress, all protocols"
  security_group_id            = aws_security_group.shared.id
  referenced_security_group_id = aws_security_group.shared.id
  ip_protocol                  = "-1"
}

resource "aws_vpc_security_group_egress_rule" "shared_all" {
  description       = "Allow all egress"
  security_group_id = aws_security_group.shared.id
  cidr_ipv4         = "0.0.0.0/0"
  ip_protocol       = "-1"
}

module "vpc_endpoints" {
  source  = "terraform-aws-modules/vpc/aws//modules/vpc-endpoints"
  version = "6.6.1"

  vpc_id             = module.vpc.vpc_id
  security_group_ids = [aws_security_group.shared.id]

  endpoints = {
    s3 = {
      service         = "s3"
      service_type    = "Gateway"
      route_table_ids = module.vpc.private_route_table_ids
      tags            = { Name = "${var.deployment_name}-s3" }
    }

    guardduty_data = {
      service             = "guardduty-data"
      service_type        = "Interface"
      private_dns_enabled = true
      subnet_ids          = module.vpc.private_subnets
      tags                = { Name = "${var.deployment_name}-guardduty-data" }
    }
  }
}
